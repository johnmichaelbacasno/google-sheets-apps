Query Board
=LET(
    status,
    IF(G$2 = "", "", QUOTE(G$2)),

    date_from,
    IF($B$6 = "", "", QUOTE_DATE($B$6)),

    date_to,
    IF($B$9 = "", "", QUOTE_DATE($B$9)),

    filter_label,
    IF($B$12 = "", "", QUOTE($B$12)),

    filter_priority,
    IF($B$15 = "", "", QUOTE($B$15)),

    filter_owner,
    IF($B$18 = "", "", QUOTE($B$18)),

    offset,
    F5,
    
    status_query,
    " M =" & status,

    date_from_query,
    IF(date_from = "", "", " AND O >= " & date_from),

    date_to_query,
    IF(date_to = "", "", " AND P <= " & date_to),

    filter_label_query,
    IF(filter_label = "", "", " AND L = " & filter_label),

    filter_priority_query,
    IF(filter_priority = "", "", " AND N = " & filter_priority),

    filter_owner_query,
    IF(filter_owner = "", "", " AND H = " & filter_owner),
    
    where_query,
    " WHERE (" & status_query & date_from_query & date_to_query & filter_label_query & filter_priority_query & filter_owner_query & ")",

    offset_query,
    IF(offset = "", "", " OFFSET " & offset),

    query_string,
    "SELECT G, H, K, L, N, Q, U" & where_query & " LIMIT 1" & offset_query,

    query_data,
    QUERY(
        List!$F$4:$W$7,
        query_string
    ),

    IF(
        ISERROR(query_data),
        "",
        LET(
            data,
            query_data,

            description,
            INDEX(data, 1),

            owner,
            INDEX(data, 2),

            color,
            INDEX(data, 3),

            label,
            INDEX(data, 4),
            
            priority,
            INDEX(data, 5),

            progress,
            INDEX(data, 6),

            due_days,
            INDEX(data, 7),

            due_info,
            IF(
                due_days = 0,
                "Due today",
                IF(
                    due_days > 0,
                    IF(
                        due_days = 1,
                        "Due tomorrow",
                        "Due in " & due_days & " days"
                    ),
                    IF(
                        due_days = -1,
                        "Due yesterday",
                        "Due " & ABS(due_days) & " days ago"
                    )
                )
            ),

            is_due,
            AND(progress < 1, due_days < 1),

            {
                SPARKLINE(1, {"charttype","bar"; "max", 1; "color1", COLOR_MAP(color)}),
                label,
                "";

                "",
                description,
                "";

                "",
                "",
                "";

                "",
                owner,
                "";

                "",
                IF(is_due, ROUND(progress * 100) & "% | " & due_info, "âœ”"),
                priority
            }
        )
    )
)

Query Board Count
=LET(
    status,
    IF(G$2 = "", "", QUOTE(G$2)),

    date_from,
    IF($B$6 = "", "", QUOTE_DATE($B$6)),

    date_to,
    IF($B$9 = "", "", QUOTE_DATE($B$9)),

    filter_label,
    IF($B$12 = "", "", QUOTE($B$12)),

    filter_priority,
    IF($B$15 = "", "", QUOTE($B$15)),

    filter_owner,
    IF($B$18 = "", "", QUOTE($B$18)),
    
    status_query,
    " M =" & status,

    date_from_query,
    IF(date_from = "", "", " AND O >= " & date_from),

    date_to_query,
    IF(date_to = "", "", " AND P <= " & date_to),

    filter_label_query,
    IF(filter_label = "", "", " AND L = " & filter_label),

    filter_priority_query,
    IF(filter_priority = "", "", " AND N = " & filter_priority),

    filter_owner_query,
    IF(filter_owner = "", "", " AND H = " & filter_owner),
    
    where_query,
    " WHERE (" & status_query & date_from_query & date_to_query & filter_label_query & filter_priority_query & filter_owner_query & ")",

    query_string,
    "SELECT COUNT(G)" & where_query,

    query_data,
    QUERY(
        List!$F$4:$W$7,
        query_string
    ),

    IFERROR(INDEX(query_data, 2), 0)
)