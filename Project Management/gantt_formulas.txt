FORMULAS

Fill First Gantt Start date
=LET(
    current_date,
    gantt.start_date,

    exclusion,
    NOT(gantt.show_days_off),

    IF(
        OR(
            current_date = "",
            Settings!weekend.string = "1111111"
        ),
        "",
        FILL_GANTT_DATE(current_date, exclusion, exclusion)
    )
)

Fill Succeeding Gantt Start dates
=LET(
    current_date,
    IF(M$2 = "", "", M$2 + 1),

    exclusion,
    NOT(gantt.show_days_off),

    IF(
        OR(
            current_date = "",
            Settings!weekend.string = "1111111"
        ),
        "",
        FILL_GANTT_DATE(current_date, exclusion, exclusion)
    )
)

Fill Gantt Week Days Initials
=IF(M$2 = "", "", LEFT(TEXT(M2, "dddd")))

Fill Gantt Week Month
=IF(M$2 = "", "", CONCATENATE(UPPER(TEXT(M2, "mmmm")), " ", UPPER(TEXT(M2, "yyyy"))))

Fill Gantt Bars
=LET(
    date,
    O$2,

    start_date,
    $K10,

    end_date,
    $L10,

    type,
    $G10,

    progress,
    $I10,
    
    IF(
        OR(date = "", AND(start_date = "", end_date = "")),
        "",
        SWITCH(
            type,
            
            types.type.phase,
            IF(
                AND(start_date <> "", end_date <> ""),
                IF(
                    ISBETWEEN(date, start_date, end_date),
                    1,
                    ""
                ),
                ""
            ),
            
            types.type.task,
            IF(
                AND(start_date <> "", end_date <> ""),
                IF(
                    ISBETWEEN(date, start_date, end_date),
                    1,
                    ""
                ),
                ""
            ),
            
            types.type.milestone,
            IF(
                end_date <> "",
                IF(
                    date = end_date,
                    IF(
                        progress = 1,
                        gantt_symbols.milestone_completed,
                        gantt_symbols.milestone_incomplete
                    ),
                    ""
                ),
                IF (
                    AND(start_date <> "", date = start_date),
                    IF(
                        progress = 1,
                        gantt_symbols.milestone_completed,
                        gantt_symbols.milestone_incomplete
                    ),
                    ""
                )
            ),

            ""
        )
    )
)

Fill Gantt Highlight Indicator
=LET(
    date,
    O$2,

    IF(
        date = "",
        "",
        IF(
            date = globals.date_today,
            "T",
            IF(
                IS_WEEKEND(date),
                "W",
                IF(
                    IS_HOLIDAY(date),
                    "H",
                    ""
                )
            )
        )
    )
)

Query Gantt Items
=LET(
    query_string,
    "SELECT G, H, I, K, O, P, Q, T, V WHERE G <> ''",

    query_data,
    QUERY(
        List!$F$4:$W$50,
        query_string
    ),

    IF(
        ISERROR(query_data),
        "",
        BYROW(
            query_data,
            LAMBDA(
                row,
                LET(
                    description,
                    INDEX(row, 1),

                    owner,
                    INDEX(row, 2),

                    type,
                    INDEX(row, 3),

                    color,
                    INDEX(row, 4),

                    start_date,
                    INDEX(row, 5),

                    end_date,
                    INDEX(row, 6),

                    progress,
                    INDEX(row, 7),

                    duration,
                    INDEX(row, 8),

                    due_in,
                    INDEX(row, 9),

                    {
                        IF(type = types.type.phase, "â–¾", ""),
                        description,
                        type,
                        color,
                        progress,
                        owner,
                        start_date,
                        end_date,
                        duration,
                        due_in
                    }
                )
            )
        )
    )
)